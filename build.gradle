buildscript {
    ext.kotlin_version = '1.3.61'
    ext.spek_version = '2.0.10'
    ext.kluent_version = '1.60'
    ext.spring_boot_version = '2.2.5.RELEASE'
    ext.lettuce_version = '5.2.2.RELEASE'
    ext.graalvm_js_version = '20.0.0'

    // Discord libraries
    ext.discord4j_version = '3.1.0-SNAPSHOT'
    ext.lavaplayer_version = '1.3.34'
    ext.reactor_version = '3.3.3.RELEASE'

    // Utility libraries
    ext.bucket4j_version = '4.6.0'
    ext.caffeine_version = '2.8.0'
    ext.jackson_version = '2.10.3'
    ext.kotlin_logging_version = '1.7.8'
    ext.logback_version = '1.2.3'
    ext.slf4j_version = '1.7.30'
    ext.micrometer_version = '1.3.6'
    ext.reactor_extensions = '1.0.2.RELEASE'

    // Build plugins
    ext.shadow_version = '5.2.0'
    ext.gradle_ktlint_version = '9.2.1'
    ext.license_plugin_version = '0.15.0'
    ext.gradle_versions_plugin = '0.28.0'
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
    id "com.github.johnrengelman.shadow" version "$shadow_version"
    id "org.jlleitschuh.gradle.ktlint" version "$gradle_ktlint_version"
    id "com.github.hierynomus.license" version "$license_plugin_version"
    id "com.github.ben-manes.versions" version "$gradle_versions_plugin"
    id "org.jetbrains.kotlin.plugin.spring" version "$kotlin_version"
    id 'org.springframework.boot' version "$spring_boot_version"
}

group 'com.github.kvnxiao'
version '0.1.0-SNAPSHOT'

sourceCompatibility = 1.8

jar {
    manifest {
        attributes 'Main-Class': 'com.github.kvnxiao.discord.MainKt'
    }
}

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.discord4j:discord4j-core:$discord4j_version"
    implementation "io.projectreactor:reactor-core:$reactor_version"
    implementation "org.springframework.boot:spring-boot-starter-webflux:$spring_boot_version"
    implementation "io.lettuce:lettuce-core:$lettuce_version"
    implementation "org.springframework.data:spring-data-redis:$spring_boot_version"
    implementation "io.micrometer:micrometer-core:$micrometer_version"
    implementation "org.slf4j:slf4j-api:$slf4j_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "io.github.microutils:kotlin-logging:$kotlin_logging_version"
    implementation "com.github.ben-manes.caffeine:caffeine:$caffeine_version"
    implementation "com.github.vladimir-bukhtoyarov:bucket4j-core:$bucket4j_version"
    implementation "com.sedmelluq:lavaplayer:$lavaplayer_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "io.projectreactor.kotlin:reactor-kotlin-extensions:$reactor_extensions"
    implementation "org.graalvm.js:js:$graalvm_js_version"
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "org.amshove.kluent:kluent:$kluent_version"
    testImplementation "io.projectreactor:reactor-test:$reactor_version"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek_version"
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs = ["-Xjsr305=strict"]
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs = ["-Xjsr305=strict"]
}

ktlint {
    version = "0.36.0"
}

license {
    header rootProject.file('docs/LICENSE_HEADER')
    strictCheck true
}

task formatAll {
    group('formatting')
    dependsOn 'licenseFormat'
    dependsOn 'ktlintFormat'
    tasks.findByName('ktlintFormat').mustRunAfter 'licenseFormat'
}
